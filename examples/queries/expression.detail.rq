SELECT DISTINCT *
WHERE {
    VALUES ?expression { <http://data.doremus.org/expression/d72301f0-0aba-3ba6-93e5-c4efbee9c6ea> }

    ?expression mus:U70_has_title ?name .
    OPTIONAL { ?expression ecrm:P3_has_note ?description }

    OPTIONAL{
      ?expression mus:U12_has_genre ?genre_uri .
      ?genre_uri skos:prefLabel ?genre_label .
      FILTER (lang(?genre_label) = 'en')
    }

    OPTIONAL{
      ?expression mus:U11_has_key ?key_uri .
      ?key_uri skos:prefLabel ?key_label .
      FILTER (lang(?key_label) = 'en')
    }

    OPTIONAL{
      ?expCreation efrbroo:R17_created ?expression .
      OPTIONAL {
        ?expCreation ecrm:P9_consists_of ?activity .
        ?activity ecrm:P14_carried_out_by ?composer_uri ;
                  mus:U31_had_function_of_type "compositeur"@fr .
      }
      OPTIONAL { ?expCreation ecrm:P4_has_time-span / time:hasBeginning / time:inXSDDate ?dateCreated }
    }

}
